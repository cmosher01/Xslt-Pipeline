plugins {
    id 'application'
    id 'java-library'
    id 'maven-publish'
}

repositories {
    mavenCentral()
    maven {
        url = uri('https://public:\u0067hp_fya6Kseu3XOBMg2icbg7f1LP6ZFYjj35v4Zj@maven.pkg.github.com/cmosher01/*')
    }
}

dependencies {
    implementation group: 'nu.mine.mosher.gnopt', name: 'gnopt', version: 'latest.release'

    runtimeOnly group: 'xerces', name: 'xercesImpl', version: 'latest.release', {
        exclude group: 'xml-apis'
    }
    runtimeOnly group: 'net.sf.saxon', name: 'Saxon-HE', version: 'latest.release', {
        exclude group: 'xml-apis'
    }
}

application {
    mainClass = 'nu.mine.mosher.xml.XsltPipelineCli'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of 11
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            pom {
                name = 'xslt-pipeline'
                description = 'Runs an XML file through a pipeline of XSLT transformations'
                url = 'https://github.com/cmosher01/Xslt-Pipeline'
                inceptionYear = '2020'
                packaging = 'jar'
                licenses {
                    license {
                        name = 'GNU General Public License v3.0'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.html'
                    }
                }
                developers {
                    developer {
                        id = 'cmosher01'
                        name = 'Christopher Alan Mosher'
                        email = 'cmosher01@gmail.com'
                        url = 'https://mosher.mine.nu'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/cmosher01/Xslt-Pipeline.git'
                    developerConnection = 'scm:git:ssh://github.com/cmosher01/Xslt-Pipeline.git'
                    url = 'http://github.com/cmosher01/Xslt-Pipeline'
                }
            }
        }
    }
}
tasks.assemble.dependsOn tasks.generatePomFileForMavenPublication

publishing {
    repositories {
        maven {
            url 'https://maven.pkg.github.com/cmosher01/xslt-pipeline'
            credentials {
                username = System.getenv('GITHUB_ACTOR') ?: 'restricted'
                password = System.getenv('GITHUB_TOKEN') ?: 'restricted'
            }
        }
    }
}

group = 'nu.mine.mosher.xml'

version = '1.2.1'
